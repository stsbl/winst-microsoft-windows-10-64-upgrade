[Initial]
Message=FÃ¼hre Windows 10 Upgrade durch...
DefVar $ExitCode$
DefVar $Progress$

[Actions]
ShowBitmap "%SystemDrive%\tmp\microsoft-windows-10-64-upgrade\logo.png" "Windows 10 Upgrade"
DosInAnIcon_Taskkill
Registry_DisableScreenSaver
DosInAnIcon_gpupdate
WinBatch_Upgrade
Sub_HandleExitCode
LinkFolder_NextStep
Files_DeleteBatch
Set $Progress$ = "2"
Registry_UpdateProgress /32bit
ExitWindows /Reboot

[DosInAnIcon_Taskkill]
; kill explorer to hide the windows gui
taskkill /IM explorer.exe /F

[WinBatch_Upgrade]
; Command Line Doc for Windows 10 Setup: https://msdn.microsoft.com/de-de/library/windows/hardware/dn938368(v=vs.85).aspx
"%SystemDrive%\tmp\microsoft-windows-10-64-upgrade\setup.exe" /auto upgrade /compat ignorewarning /dynamicupdate enable /migratedrivers all /showoobe none /telemetry disable /noreboot

[DosInAnIcon_gpupdate]
gpupdate /force

[Sub_HandleExitCode]
; check return code
Set $ExitCode$ = GetLastExitCode
comment "GetLastExitCode: " + $ExitCode$
; 0x3 = CONX_SETUP_EXITCODE_CONTINUE_REBOOT
if not($ExitCode$ = "3")
  LogError "Fatal: setup returned exit code " + $ExitCode$
  Registry_SetFailure /32bit
endif

[Registry_SetFailure]
openkey [HKEY_LOCAL_MACHINE\SOFTWARE\opsi.org\winst\microsoft-windows-10-64-upgrade]
set "Failure" = "1"

[Registry_UpdateProgress]
openkey [HKEY_LOCAL_MACHINE\SOFTWARE\opsi.org\winst\microsoft-windows-10-64-upgrade]
set "Progress" = "$Progress$"

[Registry_DisableScreensaver]
openkey [HKEY_CURRENT_USER\Software\Policies\Microsoft\Windows\Control Panel\Desktop]
set "ScreenSaveTimeOut"="0"

[LinkFolder_NextStep]
set_basefolder common_startup
set_link
  name: Windows 10 Upgrade
  target: %SystemDrive%\tmp\microsoft-windows-10-64-upgrade\opsi-client-agent.bat
end_link

[Files_DeleteBatch]
delete -fc "%CommonStartupDir%\Windows 10 Upgrade.bat"
